apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'android-aspectjx'//面向切面

aspectjx {
    exclude 'android.support'
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}
android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    resourcePrefix 'base_'
    defaultConfig {
        minSdkVersion rootProject.ext.minVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
//            对齐
            zipAlignEnabled true
//            minifyEnabled true           //混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = outputFile.name.replace(".aar", "-shetj-${defaultConfig.versionName}.aar")
                outputFileName = fileName
            }
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.google.code.gson') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "$gosnVersion"
                }
            }
            if (requested.group == 'com.google.code.findbugs') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '3.0.0'
                }
            }
            if (requested.group == 'com.github.bumptech.glide') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '4.9.0'
                }
            }
            if (requested.group == 'com.tencent.mm.opensdk') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '5.1.4'
                }
            }
        }
    }

}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation "androidx.appcompat:appcompat:$support"
//    implementation "androidx.legacy:legacy-support-v4:$androidx"
    implementation "com.google.android.material:material:$androidx"
    implementation "androidx.recyclerview:recyclerview:$androidx"
    implementation "androidx.cardview:cardview:$androidx"
    implementation "androidx.percentlayout:percentlayout:$androidx"
    implementation "androidx.core:core-ktx:$androidx"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    api 'com.github.SheTieJun:SimQUMI:6a8d9b1159'
    api 'androidx.multidex:multidex:2.0.1'
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.49-androidx'

    api 'org.simple:androideventbus:1.0.5.1'
    api "com.google.code.gson:gson:$gosnVersion"
    api 'com.github.tbruyelle:rxpermissions:0.10.2'
    //图片加载
    api 'com.github.bumptech.glide:glide:4.10.0'
    api 'com.github.iielse:ImageWatcher:1.1.5'
    //Rxlifecycle
    api 'com.trello.rxlifecycle3:rxlifecycle:3.0.0'
    api 'com.trello.rxlifecycle3:rxlifecycle-components:3.0.0'
    api 'io.reactivex.rxjava2:rxjava:2.2.13'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'com.jakewharton.rxbinding3:rxbinding:3.0.0'
    api 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    //框架
    api 'com.jakewharton.timber:timber:4.7.1'
    api 'me.jessyan:autosize:1.1.2'
    //腾讯Android开发框架
    api 'com.qmuiteam:qmui:2.0.0-alpha02'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    //https://github.com/code-mc/material-icon-lib icon图片库
    api 'net.steamcrafted:materialiconlib:1.1.5'

    //网络请求框架 https://github.com/zhou-you/RxEasyHttp
    api 'com.zhouyou:rxeasyhttp:2.1.5'
}

apply from: 'bintray.gradle'